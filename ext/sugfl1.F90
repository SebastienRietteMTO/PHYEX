SUBROUTINE SUGFL1(YDMODEL,KGFLCONF)

!     ----------------------------------------------------------------------
!**** *SUGFL1 * - Setting up GFL environment: part 1

!     Purpose.
!     --------
!      Initialization of YOM_YGFL, part 1.
!      Reads NAMGFL.
!      Does checkings on Y[X]_NL attributes.
!      If NCHEM>0 NAMCHEM is read too.

!**   Interface.
!     ----------
!        *CALL* *SUGFL1*

!        Explicit arguments :
!        --------------------
!           KGFLCONF : default configuration of GFL attributes

!        Implicit arguments :
!        --------------------

!     Method.
!     -------

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Mats Hamrud and Philippe Courtier  *ECMWF*
!      Original : 87-10-15
!      Code in SUDIM1 until 2011
!      Creation of SUGFL1 in Nov 2011.

!     Modifications.
!     --------------
!      K. YESSAD (Nov 2011) former GFL SUDIM1 code put in new SUGFL1
!      A. Inness  23 Mar 2012 Changes for CHEM fields
!      M. Diamantakis Feb-2013: Changes to support mass fixers
!      F. Vana & M. Kharoutdinov 09-Feb-2015 CRM fields
!      S. Remy 28 Jan 2015 add biomass burning emissions injection heights
!      F. Vana 20-Sep-2015 Better security for CRM usage.
!      S. Massart Dec. 2015: add linear carbon monoxide chemical scheme
!      S.-J. Lock Feb. 2017: LSPPTGFL - switch for SPPT perturbations to chemistry/aerosol tendencies
!      R. El Khatib 18-Jan-2018 optional argument KGFLCONF
!      S. Remy (Apr 2017): LVOLC_ALTI added
!      M Ahlgrimm 2017-05-11 cloud heterogeneity FSD
!      S. Remy (Nov 2017): LAERSOA added
!      K. Lonitz Apr. 2018: increased NGFL_PHYS to 9
!      M. Michou 2018-09 : add initialisation of key to ARPEGE-Climat chemistry scheme
!      P. Bechtold Feb. 2019: EDR Parameter added
!      R. El Khatib 18-Mar-2020 bugfix : give a default value to CHEM_SCHEME
!      S. Remy June 2020: LAERSOA_COUPLED added
!      M. Cussac Sept. 2023: add keys related to tropospheric chemistry (LCHEM_EMICH4, LCHEM_CSHAPE98
!     ----------------------------------------------------------------------

USE TYPE_MODEL  , ONLY : MODEL
USE PARKIND1    , ONLY : JPIM, JPRB
USE YOMHOOK     , ONLY : LHOOK, DR_HOOK, JPHOOK
USE YOMLUN      , ONLY : NULNAM, NULOUT
USE YOM_YGFL    , ONLY : JPCHEM_ASSIM, JPGHG_ASSIM, TYPE_GFL_NAML, JPAEROCV
USE YOMANEB     , ONLY : NGRBCHEM, NGRBGHGASSIM, NGRBAERCV
USE YOE_AERODIAG, ONLY : TYPE_AERO_WVL_DIAG
USE ARPCLIM_CHEM_MODULE

USE YOMCOMPO,     ONLY : AERO_SCHEME_OP_OBS, NPEMIS2D, NPEMIS2DAUX, NPEMIS3D, &
                       & TCOMPO_EMIS, COMPO_EMIS_READ_NAMELIST, &
                       & TCOMPO_EMIS_AUX, COMPO_EMIS_AUX_READ_NAMELIST

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL),INTENT(INOUT), TARGET :: YDMODEL
INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: KGFLCONF

INTEGER(KIND=JPIM) :: JGFL, ITRC, JEMIS, JEMISAUX
INTEGER(KIND=JPIM) :: IEMISAUX
INTEGER(KIND=JPIM) :: ISTAT
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------
LOGICAL, POINTER :: LCHEM_DIA, LCHEM_DDFLX, LCOMPO_DDFLX_DIR, LCHEM_TROPO, &
 & LCHEM_TL, LCHEM_LIGHT, LCHEM_DIAC, LCHEM_BASCOE_JON, LCHEM_EXTENDJNO2, &
 & LCHEM_BASCOE_HETCHEM, LMOZART_RSF_DUMMY, &
 & LCHEM_ANAO3, LCHEM_ANACH4, LCHEM_JOUT, LCHEM_FIX_CH4, LCHEM_O3RAD, LCHEM_WDFR, &
 & LCHEM_WEAK_CH4_RELAXATION, &
 & LCHEM_CONVSCAV, LCHEM_CSHAPE, LCHEM_CSHAPE98, LCHEM_AEROI, LCHEM_LCOMESO, LCHEM_LCOCSTCLIM, LCHEM_LCOLIMIT, &
 & LCOMPO_DCDD, LCHEM_VSO2_COUPLE, & 
 & LAERNITRATE, LAEREQSAM4CLIM, LCHEM_EQSAM4CLIMPH, &
 & LAERSOA, LAERSOA_COUPLED, LAERRESUSPENSION, LAERNUCL, &
 & LCHEM_ARPCLIM, LCHEM_EMICH4

REAL(KIND=JPRB), POINTER :: RCHEM_DIA_PERIOD
 
CHARACTER(LEN=20) :: CHEM_SCHEME, CHEM_LCOVERS, AERO_SCHEME
INTEGER(KIND=JPIM), POINTER :: NCHEM_LCOCOEF, KCHEM_SOLVE, KCHEM_YEARPI, KCHEM_NOXADV, KCHEM_WETDEP, KCHEM_DRYDEP, &
 & KGHG_CHEMTEND_CH4
REAL(KIND=JPRB), POINTER :: RCHEM_LCOTAUTOP, RCHEM_LCOCLIMTOP, RCHEM_LCOCOEFA1
REAL(KIND=JPRB), POINTER :: RCHEM_LINOX_SCALING, RCH4CONST

! definition of ASIS method parameters in namchem:
CHARACTER(LEN=5), POINTER   :: CSOLMET_ASIS
INTEGER(KIND=JPIM), POINTER :: M_soliter_ASIS
REAL(KIND=JPRB), POINTER    :: solcv_ASIS,RTOL_ASIS,ATOL_ASIS
INTEGER(KIND=JPIM), POINTER :: N_F_RTOL_ASIS,N_F_ATOL_ASIS

!     ------------------------------------------------------------------


TYPE(TYPE_GFL_NAML), POINTER :: &
 & YGHG_NL(:), YCHEM_NL(:), YAERO_NL(:), YERA40_NL(:), YNOGW_NL(:), YSLDIA_NL(:), &
 & YAERAOT_NL(:), YAERLISI_NL(:,:), YAEROCLIM_NL(:), YAEROUT_NL(:), YUVP_NL(:),  &
 & YFORC_NL(:), YEZDIAG_NL(:), YEXT_NL(:), YLIMA_NL(:), YEDRP_NL(:)

! If you just wrap the #if around the first line of the statement to avoid duplicating
! the list of variables, then "git ifsnorms" bails out entirely when trying to
! parse any branch touching this file. See IGT-133.
TYPE(TYPE_GFL_NAML), POINTER :: &
 & YQ_NL, YI_NL, YL_NL, &
 & YLCONV_NL, YICONV_NL, YRCONV_NL, YSCONV_NL, YIRAD_NL, YLRAD_NL, &
 & YS_NL, YR_NL, YG_NL, YH_NL, YTKE_NL, YTTE_NL, YA_NL, YO3_NL, YSRC_NL, &
 & YMXL_NL, YSHTUR_NL, YFQTUR_NL, YFSTUR_NL, YCPF_NL, YSPF_NL, YCVGQ_NL, YQVA_NL, &
 & YLRCH4_NL, YRKTH_NL, YRKTQV_NL, YRKTQC_NL,YRSPEC_NL, &
 & YSDSAT_NL, YCVV_NL, &
 & YUOM_NL, YUAL_NL, YDOM_NL, YDAL_NL, YUEN_NL, YUNEBH_NL, &
 & YEFB1_NL, YEFB2_NL, YEFB3_NL, YPHYCTY_NL, YFSD_NL, &
 & YIMF_NL,YLMF_NL,YAMF_NL,YWMFC_NL,YHLMF_NL,YHLCFMF_NL, &
 & YHIMF_NL,YHICFMF_NL

TYPE(TYPE_AERO_WVL_DIAG), POINTER :: YAERO_WVL_DIAG_NL(:)

INTEGER(KIND=JPIM), POINTER :: NGFL_EXT, NGHG, NAERO, NCHEM, &
 & NACTAERO, NERA40, NOPTMFBC, NOPTMFPR, NOPTVFE, NOPTNEGFIX, NMFDIAGLEV, &
 & NGFL_FORC, NGFL_EZDIAG, NSLDIA, NAEROUT, NAEROCLIM, NUVP, NLIMA, &
 & NAERO_WVL_DIAG, NAERO_WVL_DIAG_TYPES, NEDRP
LOGICAL, POINTER :: LAERCHEM, LTRCMFIX, LTRCMFIX_PS, LTRCMFBC, LTRCMFPR, LTRCMFMG, LEXTRADF, &
 & LAERAOT, LAERLISI, LAEROUT, LUVPOUT, LAEROCLIM, &
 & LTRCMFP, LTRCMFQM, LTRCMFA_DIF, LTRCMFA_VER, LTRCMFA_LAP, LQM3DCONS, LSPPTGFL

TYPE(TCOMPO_EMIS) :: YEMIS_TMP
TYPE(TCOMPO_EMIS_AUX) :: YEMIS_AUX_TMP

!     ------------------------------------------------------------------

#include "namgfl.nam.h"
#include "namcompo.nam.h"
#include "namchem.nam.h"
 
#include "posnam.intfb.h"
#include "posname.intfb.h"
#include "suctrl_gflattr.intfb.h"
#include "sudefo_gflattr.intfb.h"
#include "su_aerw.intfb.h"


!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUGFL1',0,ZHOOK_HANDLE)
ASSOCIATE(YGFL=>YDMODEL%YRML_GCONF%YGFL,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY, &
 & YDCOMPO=>YDMODEL%YRML_CHEM%YRCOMPO,YDCHEM=>YDMODEL%YRML_CHEM%YRCHEM)
ASSOCIATE(NCHEM_ASSIM=>YGFL%NCHEM_ASSIM, NCHEM_DV=>YGFL%NCHEM_DV,&
 & NCHEM_FLXO=>YGFL%NCHEM_FLXO, NCHEM_TC=>YGFL%NCHEM_TC, &
 & NCHEM_WDFLX=>YGFL%NCHEM_WDFLX, NCHEM_DDFLX=>YGFL%NCHEM_DDFLX, &
 & NCHEM_SCV=>YGFL%NCHEM_SCV, NGEMS=>YGFL%NGEMS, &
 & NEMIS2D=>YGFL%NEMIS2D, NEMIS2DAUX=>YGFL%NEMIS2DAUX, NEMIS3D=>YGFL%NEMIS3D, &
 & NSLDIAGP=>YGFL%NSLDIAGP,NGHG_ASSIM=>YGFL%NGHG_ASSIM, NAEROCV=>YGFL%NAEROCV)

! Associate pointers for variables in namelist NAMCOMPO
LCHEM_DIA        => YDCOMPO%LCHEM_DIA
LCHEM_DDFLX      => YDCOMPO%LCHEM_DDFLX
LCOMPO_DDFLX_DIR => YDCOMPO%LCOMPO_DDFLX_DIR
LCHEM_TROPO      => YDCOMPO%LCHEM_TROPO
LCOMPO_DCDD      => YDCOMPO%LCOMPO_DCDD
LAERNITRATE      => YDCOMPO%LAERNITRATE
LAEREQSAM4CLIM      => YDCOMPO%LAEREQSAM4CLIM
LCHEM_EQSAM4CLIMPH  => YDCOMPO%LCHEM_EQSAM4CLIMPH
LAERRESUSPENSION      => YDCOMPO%LAERRESUSPENSION
LAERSOA          => YDCOMPO%LAERSOA
LAERSOA_COUPLED  => YDCOMPO%LAERSOA_COUPLED
LAERNUCL      => YDCOMPO%LAERNUCL
RCHEM_DIA_PERIOD => YDCOMPO%RCHEM_DIA_PERIOD
KGHG_CHEMTEND_CH4=> YDCOMPO%KGHG_CHEMTEND_CH4

! Associate pointers for variables in namelist NAMCHEM
LCHEM_LIGHT      => YDCHEM%LCHEM_LIGHT
RCHEM_LINOX_SCALING => YDCHEM%RCHEM_LINOX_SCALING
LCHEM_DIAC       => YDCHEM%LCHEM_DIAC
KCHEM_NOXADV     => YDCHEM%KCHEM_NOXADV
KCHEM_WETDEP     => YDCHEM%KCHEM_WETDEP
LCHEM_ANAO3      => YDCHEM%LCHEM_ANAO3
LCHEM_ANACH4     => YDCHEM%LCHEM_ANACH4
LCHEM_JOUT       => YDCHEM%LCHEM_JOUT
LCHEM_BASCOE_JON => YDCHEM%LCHEM_BASCOE_JON
LCHEM_BASCOE_HETCHEM=> YDCHEM%LCHEM_BASCOE_HETCHEM
LMOZART_RSF_DUMMY=> YDCHEM%LMOZART_RSF_DUMMY
LCHEM_EXTENDJNO2 => YDCHEM%LCHEM_EXTENDJNO2
LCHEM_FIX_CH4    => YDCHEM%LCHEM_FIX_CH4
RCH4CONST        => YDCHEM%RCH4CONST
LCHEM_O3RAD      => YDCHEM%LCHEM_O3RAD
LCHEM_AEROI      => YDCHEM%LCHEM_AEROI
LCHEM_WDFR       => YDCHEM%LCHEM_WDFR
LCHEM_CONVSCAV   => YDCHEM%LCHEM_CONVSCAV
LCHEM_CSHAPE     => YDCHEM%LCHEM_CSHAPE
LCHEM_CSHAPE98   => YDCHEM%LCHEM_CSHAPE98
KCHEM_SOLVE      => YDCHEM%KCHEM_SOLVE
KCHEM_YEARPI     => YDCHEM%KCHEM_YEARPI
NCHEM_LCOCOEF    => YDCHEM%NCHEM_LCOCOEF
LCHEM_LCOMESO    => YDCHEM%LCHEM_LCOMESO
LCHEM_LCOCSTCLIM => YDCHEM%LCHEM_LCOCSTCLIM
RCHEM_LCOTAUTOP  => YDCHEM%RCHEM_LCOTAUTOP
RCHEM_LCOCLIMTOP => YDCHEM%RCHEM_LCOCLIMTOP
LCHEM_LCOLIMIT   => YDCHEM%LCHEM_LCOLIMIT
RCHEM_LCOCOEFA1  => YDCHEM%RCHEM_LCOCOEFA1
LCHEM_ARPCLIM    => YDCHEM%LCHEM_ARPCLIM
LCHEM_EMICH4     => YDCHEM%LCHEM_EMICH4  
LCHEM_TL         => YDCHEM%LCHEM_TL
LCHEM_VSO2_COUPLE=> YDCHEM%LCHEM_VSO2_COUPLE
LCHEM_WEAK_CH4_RELAXATION => YDCHEM%LCHEM_WEAK_CH4_RELAXATION
KCHEM_DRYDEP     => YDCHEM%KCHEM_DRYDEP

! definition of ASIS method parameters in namchem:
CSOLMET_ASIS => YDCHEM%CSOLMET_ASIS 
M_soliter_ASIS => YDCHEM%M_soliter_ASIS 
solcv_ASIS => YDCHEM%solcv_ASIS 
RTOL_ASIS => YDCHEM%RTOL_ASIS 
N_F_RTOL_ASIS => YDCHEM%N_F_RTOL_ASIS 
ATOL_ASIS => YDCHEM%ATOL_ASIS 
N_F_ATOL_ASIS => YDCHEM%N_F_ATOL_ASIS 

! Associate pointers for variables in namelist NAMGFL
YQ_NL          => YGFL%YQ_NL
YI_NL          => YGFL%YI_NL
YL_NL          => YGFL%YL_NL
YLCONV_NL      => YGFL%YLCONV_NL
YICONV_NL      => YGFL%YICONV_NL
YRCONV_NL      => YGFL%YRCONV_NL
YSCONV_NL      => YGFL%YSCONV_NL
YIRAD_NL       => YGFL%YIRAD_NL
YLRAD_NL       => YGFL%YLRAD_NL
YS_NL          => YGFL%YS_NL
YR_NL          => YGFL%YR_NL
YG_NL          => YGFL%YG_NL
YH_NL          => YGFL%YH_NL
YTKE_NL        => YGFL%YTKE_NL
YTTE_NL        => YGFL%YTTE_NL
YEFB1_NL       => YGFL%YEFB1_NL
YEFB2_NL       => YGFL%YEFB2_NL
YEFB3_NL       => YGFL%YEFB3_NL
YA_NL          => YGFL%YA_NL
YO3_NL         => YGFL%YO3_NL
YSRC_NL        => YGFL%YSRC_NL
YIMF_NL        => YGFL%YIMF_NL
YLMF_NL        => YGFL%YLMF_NL
YAMF_NL        => YGFL%YAMF_NL
YWMFC_NL       => YGFL%YWMFC_NL
YHLMF_NL       => YGFL%YHLMF_NL
YHLCFMF_NL     => YGFL%YHLCFMF_NL
YHIMF_NL       => YGFL%YHIMF_NL
YHICFMF_NL     => YGFL%YHICFMF_NL
YMXL_NL        => YGFL%YMXL_NL
YSHTUR_NL      => YGFL%YSHTUR_NL
YFQTUR_NL      => YGFL%YFQTUR_NL
YFSTUR_NL      => YGFL%YFSTUR_NL
YCPF_NL        => YGFL%YCPF_NL
YSPF_NL        => YGFL%YSPF_NL
YCVGQ_NL       => YGFL%YCVGQ_NL
YQVA_NL        => YGFL%YQVA_NL
YGHG_NL        => YGFL%YGHG_NL
YCHEM_NL       => YGFL%YCHEM_NL
YAERO_NL       => YGFL%YAERO_NL
YERA40_NL      => YGFL%YERA40_NL
YNOGW_NL       => YGFL%YNOGW_NL
YEDRP_NL       => YGFL%YEDRP_NL
YPHYCTY_NL     => YGFL%YPHYCTY_NL
YSLDIA_NL      => YGFL%YSLDIA_NL
YLRCH4_NL      => YGFL%YLRCH4_NL
YAERAOT_NL     => YGFL%YAERAOT_NL
YAERLISI_NL    => YGFL%YAERLISI_NL
YAEROUT_NL     => YGFL%YAEROUT_NL
YAEROCLIM_NL   => YGFL%YAEROCLIM_NL
YUVP_NL        => YGFL%YUVP_NL
YRKTH_NL       => YGFL%YRKTH_NL
YRSPEC_NL      => YGFL%YRSPEC_NL
YRKTQV_NL      => YGFL%YRKTQV_NL
YRKTQC_NL      => YGFL%YRKTQC_NL
YSDSAT_NL      => YGFL%YSDSAT_NL
YCVV_NL        => YGFL%YCVV_NL
YFORC_NL       => YGFL%YFORC_NL
YEZDIAG_NL     => YGFL%YEZDIAG_NL
YEXT_NL        => YGFL%YEXT_NL
YFSD_NL        => YGFL%YFSD_NL
YUOM_NL        => YGFL%YUOM_NL
YUAL_NL        => YGFL%YUAL_NL
YDOM_NL        => YGFL%YDOM_NL
YDAL_NL        => YGFL%YDAL_NL
YUEN_NL        => YGFL%YUEN_NL
YUNEBH_NL      => YGFL%YUNEBH_NL
YDOM_NL        => YGFL%YDOM_NL
YAERO_WVL_DIAG_NL => YGFL%YAERO_WVL_DIAG_NL
YLIMA_NL       => YGFL%YLIMA_NL

NGFL_EXT       => YGFL%NGFL_EXT
NGHG           => YGFL%NGHG
NAERO          => YGFL%NAERO
NCHEM          => YGFL%NCHEM
NACTAERO       => YGFL%NACTAERO
NERA40         => YGFL%NERA40
NEDRP          => YGFL%NEDRP
NOPTMFBC       => YGFL%NOPTMFBC
NOPTMFPR       => YGFL%NOPTMFPR
NOPTVFE        => YGFL%NOPTVFE
NOPTNEGFIX     => YGFL%NOPTNEGFIX
NMFDIAGLEV     => YGFL%NMFDIAGLEV
NGFL_FORC      => YGFL%NGFL_FORC
NGFL_EZDIAG    => YGFL%NGFL_EZDIAG 
NSLDIA         => YGFL%NSLDIA
NAEROUT        => YGFL%NAEROUT
NAEROCLIM      => YGFL%NAEROCLIM
NUVP           => YGFL%NUVP
NAERO_WVL_DIAG => YGFL%NAERO_WVL_DIAG
NAERO_WVL_DIAG_TYPES => YGFL%NAERO_WVL_DIAG_TYPES
LAERCHEM       => YGFL%LAERCHEM
LTRCMFIX       => YGFL%LTRCMFIX
LTRCMFIX_PS    => YGFL%LTRCMFIX_PS
LTRCMFQM       => YGFL%LTRCMFQM
LTRCMFBC       => YGFL%LTRCMFBC
LTRCMFPR       => YGFL%LTRCMFPR
LTRCMFMG       => YGFL%LTRCMFMG
LTRCMFP        => YGFL%LTRCMFP
LTRCMFA_DIF    => YGFL%LTRCMFA_DIF
LTRCMFA_VER    => YGFL%LTRCMFA_VER
LTRCMFA_LAP    => YGFL%LTRCMFA_LAP
LEXTRADF       => YGFL%LEXTRADF
LAERAOT        => YGFL%LAERAOT
LAERLISI       => YGFL%LAERLISI
LAEROUT        => YGFL%LAEROUT
LAEROCLIM      => YGFL%LAEROCLIM
LUVPOUT        => YGFL%LUVPOUT  
LQM3DCONS      => YGFL%LQM3DCONS
LSPPTGFL       => YGFL%LSPPTGFL
NLIMA          => YGFL%NLIMA

!     ------------------------------------------------------------------

!*       1. SET DEFAULT VALUES.
!           -------------------

!        1.1  Set implicit default values for GFL dimensions

NERA40=0
IF ( YDMODEL%YRML_DYN%YRDYNA%LSLDIA ) THEN
  NSLDIA=7
  NSLDIAGP=1
ELSE
  NSLDIA=0
  NSLDIAGP=0
ENDIF
NGHG=0
NAERO=0
NACTAERO=0
NAEROUT=0
NAEROCLIM=0
NUVP=0
NGFL_FORC=0
NGFL_EZDIAG=0
NGFL_EXT=0
NCHEM=0
NCHEM_ASSIM=0
NGHG_ASSIM=0
NAEROCV=0
NCHEM_FLXO=0
NCHEM_WDFLX=0
NCHEM_DDFLX=0
NCHEM_DV=0
NCHEM_SCV=0
NCHEM_TC=0
NEMIS2D=0
NEMIS2DAUX=0
NEMIS3D=0
NLIMA=0

!        1.2  Set implicit default values for logical variables other than GFL attributes
LAERAOT=.FALSE.
LAERLISI=.FALSE.
LAEROUT=.FALSE.
LAEROCLIM=.FALSE.
LUVPOUT=.FALSE.
LTRCMFIX=.FALSE.
LTRCMFIX_PS=.FALSE.
LTRCMFQM=.FALSE.
LTRCMFBC=.TRUE.
LEXTRADF=.FALSE.
LTRCMFPR=.FALSE.
LTRCMFMG=.FALSE.
LTRCMFP=.FALSE.
LTRCMFA_DIF=.FALSE.
LTRCMFA_VER=.FALSE.
LTRCMFA_LAP=.FALSE.
LSPPTGFL=.FALSE.
NOPTMFBC=3
NOPTMFPR=1
NOPTNEGFIX=1
NOPTVFE=0
NMFDIAGLEV=0

!        1.3  Set implicit default values for GFL attributes
!             All the GFL attributes defaults computed before reading the
!             namelist NAMGFL SHOULD be defined in SUDEFO_GFLATTR

CALL SUDEFO_GFLATTR(YDMODEL,KGFLCONF)

!     ------------------------------------------------------------------

!*       2. READ NAMELIST.
!           --------------

CALL POSNAM(NULNAM,'NAMGFL')
READ(NULNAM,NAMGFL)

LTRCMFIX=LTRCMFP.OR.LTRCMFMG.OR.LTRCMFBC.OR.LTRCMFPR.OR.LTRCMFA_DIF.OR.&
   &     LTRCMFA_VER.OR.LTRCMFA_LAP

!Switch for SPPT perturbations of CHEM tendencies
!**NOTE: These are ONLY active if SPPT (LSPSDT) is also active
LSPPTGFL=LSPPTGFL.AND.(NCHEM+NGHG+NAERO>0)

!     ------------------------------------------------------------------

!*       3. CHECKINGS.
!           ----------

CALL SUCTRL_GFLATTR(YDMODEL%YRML_PHY_EC%YREPHY,YDMODEL%YRML_GCONF, &
 & YDMODEL%YRML_PHY_MF%YRARPHY,YDMODEL%YRML_PHY_MF%YRPHY, YDMODEL%YRML_PHY_MF%YRSIMPHL,&
 & YDMODEL%YRML_DYN%YRDYNA)

!     ------------------------------------------------------------------

!*       6. SOME PRINTINGS
!           --------------
WRITE(NULOUT,'('' NGFL_EXT = '',I4,'' NLIMA = '',I4)') NGFL_EXT, NLIMA

IF (LTRCMFIX) THEN
  IF (LTRCMFBC) THEN
    WRITE(NULOUT,*) 'BERMEJO & CONDE MASS FIXING SCHEME IS ON'
  ELSEIF (LTRCMFPR) THEN
    WRITE(NULOUT,*) 'PRIESTLEY MASS FIXING SCHEME IS ON'
  ELSEIF (LTRCMFMG) THEN
    WRITE(NULOUT,*) 'MAC GREGOR MASS FIXING SCHEME IS ON'
  ELSEIF (LTRCMFP) THEN
    WRITE(NULOUT,*) 'PROPORTIONAL MASS FIXING SCHEME IS ON'
  ENDIF
  IF ( NOPTVFE == 1 ) THEN
    WRITE(NULOUT,*) 'VERTICAL FE IS IN USE IN TOTAL MASS BUDGET CALCULATIONS'
  ELSE
    WRITE(NULOUT,*) 'FINITE DIFFERENCES USED IN TOTAL MASS BUDGET CALCULATIONS'
  ENDIF
ENDIF

WRITE(NULOUT,'(A,L2,A,L2,A,L2,A,L2,A,L2,A,L2)')&
  & '  LTRCMFP=',LTRCMFP,&
  & '  LTRCMFBC=',LTRCMFBC,&
  & '  LTRCMFPR=',LTRCMFPR,&
  & '  LTRCMFMG=',LTRCMFMG,&
  & '  LTRCMFA_DIF=',LTRCMFA_DIF,&
  & '  LTRCMFIX_PS=',LTRCMFIX_PS,&
  & '  LTRCMFQM=',LTRCMFQM

WRITE(NULOUT,'(A,L2)') '  LQM3DCONS=',LQM3DCONS

!     ------------------------------------------------------------------

!*       7. ADDITIONAL SETTINGS FOR COMPOSITION AND CHEMISTRY NAMELIST"
!           -------------------------------------------------------

! this may be put in separate routine 
! 
! Composition namelist 
 
LCHEM_DIA=.FALSE.
RCHEM_DIA_PERIOD=6.0_JPRB
LCHEM_DDFLX=.FALSE.
LCOMPO_DDFLX_DIR=.FALSE.
LCHEM_TROPO=.FALSE.
LCOMPO_DCDD=.TRUE.
LAERNITRATE=.FALSE.
LAEREQSAM4CLIM=.FALSE.
LCHEM_EQSAM4CLIMPH=.FALSE.
LAERRESUSPENSION=.FALSE.
LAERSOA=.FALSE.
LAERSOA_COUPLED=.FALSE.
LAERNUCL=.TRUE.
KGHG_CHEMTEND_CH4=0_JPIM
!
AERO_SCHEME="aer"
!
WRITE(NULOUT,*) ' BEFORE READ NAMCOMP ' 
IF (NCHEM+NGHG+NAERO > 0 ) THEN 
  CALL POSNAM(NULNAM,'NAMCOMPO')
  READ(NULNAM,NAMCOMPO)

  AERO_SCHEME_OP_OBS = AERO_SCHEME

  WRITE(NULOUT,'(12(A,L2),A,I2)')&
  & '  LCHEM_DIA=',LCHEM_DIA,&
  & '  LCHEM_DDFLX=',LCHEM_DDFLX,&
  & '  LCOMPO_DDFLX_DIR=',LCOMPO_DDFLX_DIR,&
  & '  LCHEM_TROPO=',LCHEM_TROPO,&
  & '  LCOMPO_DCDD=',LCOMPO_DCDD,&
  & '  LAERNITRATE=',LAERNITRATE, &
  & '  LCHEM_EQSAM4CLIMPH=',LCHEM_EQSAM4CLIMPH, &
  & '  LAEREQSAM4CLIM=',LAEREQSAM4CLIM, &
  & '  LAERRESUSPENSION=',LAERRESUSPENSION, &
  & '  LAERSOA=',LAERSOA, &
  & '  LAERSOA_COUPLED=',LAERSOA_COUPLED, &
  & '  LAERNUCL=',LAERNUCL, &
  & '  KGHG_CHEMTEND_CH4=',KGHG_CHEMTEND_CH4
  WRITE(NULOUT,'(A,f8.3)') &
  & '  RCHEM_DIA_PERIOD=',RCHEM_DIA_PERIOD
  WRITE(NULOUT,'(A,A10)') &
  & '  AERO_SCHEME=',AERO_SCHEME


  ! check if chemistry is activated if LAERNITRATE
  IF (NCHEM == 0 .AND. LAERNITRATE) THEN
     CALL ABOR1('SUGFL1: LAERNITRATE requires chemistry')
  ENDIF
  IF (NCHEM == 0 .AND. LAEREQSAM4CLIM) THEN
     CALL ABOR1('SUGFL1: LAEREQSAM4CLIM requires chemistry')
  ENDIF
  IF (LCHEM_EQSAM4CLIMPH .AND. .NOT. LAEREQSAM4CLIM) THEN
     CALL ABOR1('SUGFL1: LCHEM_EQSAM4CLIMPH requires LAEREQSAM4CLIM')
  ENDIF
  IF (.NOT. LAERNITRATE .AND. LAEREQSAM4CLIM) THEN
     CALL ABOR1('SUGFL1: LAEREQSAM4CLIM requires LAERNITRATE')
  ENDIF

  YDCOMPO%AERO_SCHEME  = AERO_SCHEME

! array position for air craft emissions and tropopause height
! (these won't get set in chem_init if NCHEM is zero)
  IF (LCHEM_DIA .AND. NCHEM == 0) THEN 
       YDCHEM%IEXTR_FREE(1,5) = NGHG + NACTAERO + 1
       YDCHEM%IEXTR_FREE(1,6) = NGHG + NACTAERO + 2  
  ENDIF

  YDCOMPO%NEMIS2D_DESC=0
  YDCOMPO%NEMIS3D_DESC=0

  ! Read in flexible emissions namelists
  CALL POSNAME(NULNAM,'NAMCOMPO_EMIS',ISTAT)
  SELECT CASE (ISTAT)
    CASE (0)
      ALLOCATE(YDCOMPO%YEMIS2D_DESC(NPEMIS2D))
      ALLOCATE(YDCOMPO%YEMIS3D_DESC(NPEMIS3D))
      DO WHILE(.TRUE.)
        IF ( COMPO_EMIS_READ_NAMELIST(YEMIS_TMP) ) THEN
          IF (YEMIS_TMP%VERTICAL_PROFILE_TYPE == '3D') THEN
            IF ( YDCOMPO%NEMIS3D_DESC >= NPEMIS3D ) THEN
              CALL ABOR1 ('SUGFL1:TOO MANY NAMCOMPO_EMIS NAMELISTS - INCREASE NPEMIS3D')
            ENDIF

            IF (YEMIS_TMP%TEMPORALITY == 'MCC') THEN
              CALL ABOR1('SUGFL1: MCC TEMPORALITY not supported in YEMIS3D_DESC')
            ENDIF

            ! Is it a new field or a duplicate?
            DO JEMIS=1,YDCOMPO%NEMIS3D_DESC
              IF (YDCOMPO%YEMIS3D_DESC(JEMIS)%PARAMID == YEMIS_TMP%PARAMID) THEN
                YEMIS_TMP%PARAM_INDEX = YDCOMPO%YEMIS3D_DESC(JEMIS)%PARAM_INDEX
                EXIT
              ENDIF
            ENDDO
            IF (YEMIS_TMP%PARAM_INDEX <= 0) THEN
              YGFL%NEMIS3D = YGFL%NEMIS3D + 1
              YEMIS_TMP%PARAM_INDEX = YGFL%NEMIS3D
            ENDIF

            YDCOMPO%NEMIS3D_DESC = YDCOMPO%NEMIS3D_DESC + 1
            YDCOMPO%YEMIS3D_DESC(YDCOMPO%NEMIS3D_DESC) = YEMIS_TMP

            WRITE(NULOUT,*) 'READ YEMIS3D_DESC', YDCOMPO%NEMIS3D_DESC, YDCOMPO%YEMIS3D_DESC(YDCOMPO%NEMIS3D_DESC)
          ELSE
            IF ( YDCOMPO%NEMIS2D_DESC >= NPEMIS2D ) THEN
              CALL ABOR1 ('SUGFL1:TOO MANY NAMCOMPO_EMIS NAMELISTS - INCREASE NPEMIS2D')
            ENDIF
            ! Is it a new field or a duplicate?
            DO JEMIS=1,YDCOMPO%NEMIS2D_DESC
              IF (YDCOMPO%YEMIS2D_DESC(JEMIS)%PARAMID == YEMIS_TMP%PARAMID) THEN
                YEMIS_TMP%PARAM_INDEX = YDCOMPO%YEMIS2D_DESC(JEMIS)%PARAM_INDEX
                EXIT
              ENDIF
            ENDDO
            IF (YEMIS_TMP%PARAM_INDEX <= 0) THEN
              YGFL%NEMIS2D = YGFL%NEMIS2D + 1
              YEMIS_TMP%PARAM_INDEX = YGFL%NEMIS2D
            ELSE
              IF (YEMIS_TMP%TEMPORALITY /= YDCOMPO%YEMIS2D_DESC(JEMIS)%TEMPORALITY) THEN
                CALL ABOR1('SUGFL1: inconsistent TEMPORALITY in YEMIS2D_DESC')
              ENDIF
            ENDIF

            YDCOMPO%NEMIS2D_DESC=YDCOMPO%NEMIS2D_DESC + 1
            YDCOMPO%YEMIS2D_DESC(YDCOMPO%NEMIS2D_DESC) = YEMIS_TMP
            WRITE(NULOUT,*) 'READ YEMIS2D_DESC', YDCOMPO%NEMIS2D_DESC, YDCOMPO%YEMIS2D_DESC(YDCOMPO%NEMIS2D_DESC)
          ENDIF
        ELSE
          EXIT
        ENDIF
      ENDDO
      WRITE(NULOUT,*) 'NEMIS2D_DESC=', YDCOMPO%NEMIS2D_DESC, 'NEMIS3D_DESC=', YDCOMPO%NEMIS3D_DESC
    CASE (1)
        ! No NAMCOMPO_EMIS namelists present
    CASE DEFAULT
      CALL ABOR1 ('SUGFL1:ERROR LOOKING FOR NAMCOMPO_EMIS')
  END SELECT

  ! Set up auxiliary emission field specifications
  CALL POSNAME(NULNAM,'NAMCOMPO_EMIS_AUX',ISTAT)
  SELECT CASE (ISTAT)
    CASE (0)
      ALLOCATE(YDCOMPO%YEMIS2DAUX_DESC(NPEMIS2DAUX))
      DO WHILE(.TRUE.)
        IF ( COMPO_EMIS_AUX_READ_NAMELIST(YEMIS_AUX_TMP) ) THEN
          IF ( YGFL%NEMIS2DAUX >= NPEMIS2DAUX ) THEN
            CALL ABOR1 ('SUGFL1:TOO MANY NAMCOMPO_EMIS_AUX NAMELISTS - INCREASE NPEMIS2D_AUX')
          ENDIF
          ! Is it a new field or a duplicate?
          IF (ANY(YDCOMPO%YEMIS2DAUX_DESC(1:YGFL%NEMIS2DAUX)%PARAMID == YEMIS_AUX_TMP%PARAMID)) THEN
            CALL ABOR1('SUGFL1:DUPLICATE PARAMID IN NAMCOMPO_EMIS_AUX')
          ENDIF

          YGFL%NEMIS2DAUX=YGFL%NEMIS2DAUX + 1
          YDCOMPO%YEMIS2DAUX_DESC(YGFL%NEMIS2DAUX) = YEMIS_AUX_TMP
          WRITE(NULOUT,*) 'READ YEMIS2DAUX_DESC', YGFL%NEMIS2DAUX, YDCOMPO%YEMIS2DAUX_DESC(YGFL%NEMIS2DAUX)
        ELSE
          EXIT
        ENDIF
      ENDDO
      WRITE(NULOUT,*) 'NEMIS2D_AUX=', YGFL%NEMIS2DAUX
    CASE (1)
        ! No NAMCOMPO_EMIS namelists present
    CASE DEFAULT
      CALL ABOR1 ('SUGFL1:ERROR LOOKING FOR NAMCOMPO_EMIS_AUX')
  END SELECT

  IF (YDCOMPO%NEMIS2D_DESC > 0) THEN
    IF (ANY(YDCOMPO%YEMIS2D_DESC(1:YDCOMPO%NEMIS2D_DESC)%VERTICAL_PARAMID > 0)) THEN
      IF (YGFL%NEMIS2DAUX == 0) ALLOCATE(YDCOMPO%YEMIS2DAUX_DESC(NPEMIS2DAUX))
      DO JEMIS=1,YDCOMPO%NEMIS2D_DESC
        IF (YDCOMPO%YEMIS2D_DESC(JEMIS)%VERTICAL_PARAMID > 0) THEN
          ! This is FINDLOC, but not all compilers have it...
          IEMISAUX=0
          DO JEMISAUX=1,YGFL%NEMIS2DAUX
            IF (YDCOMPO%YEMIS2DAUX_DESC(JEMISAUX)%PARAMID == YDCOMPO%YEMIS2D_DESC(JEMIS)%VERTICAL_PARAMID) THEN
              IEMISAUX=JEMISAUX
              EXIT
            ENDIF
          ENDDO
          IF (IEMISAUX == 0) THEN
            YGFL%NEMIS2DAUX=YGFL%NEMIS2DAUX+1
            IEMISAUX=YGFL%NEMIS2DAUX
            YDCOMPO%YEMIS2DAUX_DESC(IEMISAUX)%PARAMID=YDCOMPO%YEMIS2D_DESC(JEMIS)%VERTICAL_PARAMID
            YDCOMPO%YEMIS2DAUX_DESC(IEMISAUX)%TEMPORALITY=YDCOMPO%YEMIS2D_DESC(JEMIS)%TEMPORALITY
            WRITE(YDCOMPO%YEMIS2DAUX_DESC(IEMISAUX)%CNAME, '(A8,I6)') 'EM2DAUX ', YDCOMPO%YEMIS2DAUX_DESC(IEMISAUX)%PARAMID
            WRITE(NULOUT,*) 'EMIS2D', JEMIS, ': ADD YEMIS2DAUX_DESC', IEMISAUX, YDCOMPO%YEMIS2DAUX_DESC(IEMISAUX)
          ELSE
            WRITE(NULOUT,*) 'EMIS2D', JEMIS, ': RE-USE YEMIS2DAUX_DESC', IEMISAUX, YDCOMPO%YEMIS2DAUX_DESC(IEMISAUX)
          ENDIF
          YDCOMPO%YEMIS2D_DESC(JEMIS)%VERTICAL_PARAM_INDEX = IEMISAUX
        ENDIF
      ENDDO
    ENDIF
    WRITE(NULOUT,*) 'NEMIS2DAUX=', YGFL%NEMIS2DAUX
  ELSE
    YGFL%NEMIS2DAUX = 0
  ENDIF
ELSE
  LCHEM_DIA=.FALSE.
ENDIF 

NGRBCHEM = -999
NGRBGHGASSIM = -999
NGRBAERCV = -999

! GHG namelist

IF(NGHG > 0 ) THEN
  DO JGFL = 1, NGHG
    IF (YGHG_NL(JGFL)%LASSIM       ) THEN
      NGHG_ASSIM=NGHG_ASSIM + 1
      IF (NGHG_ASSIM > JPGHG_ASSIM) CALL ABOR1('SUDIM1: Too many assimilated GHG fields. Increase JPGHG_ASSIM.')
      NGRBGHGASSIM(NGHG_ASSIM) = YGHG_NL(JGFL)%IGRBCODE
    ENDIF
  ENDDO
ENDIF

! CHEM namelist 
IF(NCHEM > 0 ) THEN
  DO JGFL = 1, NCHEM
    IF (YCHEM_NL(JGFL)%IGRIBDV  > 0 ) NCHEM_DV=NCHEM_DV + 1
    IF (YCHEM_NL(JGFL)%IGRBFLXO > 0 ) NCHEM_FLXO=NCHEM_FLXO + 1
    IF (YCHEM_NL(JGFL)%IGRBWDFLX > 0 ) NCHEM_WDFLX=NCHEM_WDFLX + 1
    IF (YCHEM_NL(JGFL)%IGRBDDFLX > 0 ) NCHEM_DDFLX=NCHEM_DDFLX + 1
    IF (YCHEM_NL(JGFL)%IGRIBTC  > 0 ) NCHEM_TC=NCHEM_TC + 1
    IF (YCHEM_NL(JGFL)%HENRYA   > 0 ) NCHEM_SCV=NCHEM_SCV + 1
    IF (YCHEM_NL(JGFL)%LASSIM       ) THEN
      NCHEM_ASSIM=NCHEM_ASSIM + 1
      IF (NCHEM_ASSIM >JPCHEM_ASSIM) CALL ABOR1('SUDIM1: Too many assimilated CHEM fields. Increase JPCHEM_ASSIM.')
      NGRBCHEM(NCHEM_ASSIM) = YCHEM_NL(JGFL)%IGRBCODE
    ENDIF
  ENDDO
  WRITE(NULOUT,*) 'NCHEM_DV=',NCHEM_DV,' NCHEM_FLXO=',&
   & NCHEM_FLXO,'NCHEM_WDFLX',NCHEM_WDFLX,'NCHEM_DDFLX',NCHEM_DDFLX,' NCHEM_TC=',&
   & NCHEM_TC,' NCHEM_SCV=',NCHEM_SCV,' NCHEM_ASSIM=', NCHEM_ASSIM
  LCHEM_LIGHT=.FALSE.

  ! scaling factor for NOx lightning. ZLI_SCAL=1.0 seems to lead to too high
  ! NOx emissions (~8.5 Tg N). Scaling by factor 0.6 to optimize to 5. Tg N/yr
  ! Value for Meijer Scheme
  ! old figure
  !RCHEM_LINOX_SCALING=0.6_JPRB
  ! updated scheme with same ic and ctg effectivity
  ! NLIMODE=4
  !RCHEM_LINOX_SCALING=2.1_JPRB
  ! NLIMODE=6
  RCHEM_LINOX_SCALING=2.5_JPRB
  KCHEM_DRYDEP=1

  LCHEM_DIAC=.FALSE.
  LCHEM_JOUT=.FALSE.
  LCHEM_BASCOE_JON=.FALSE.
  LCHEM_BASCOE_HETCHEM=.TRUE.
  LMOZART_RSF_DUMMY=.FALSE.
  LCHEM_EXTENDJNO2=.TRUE.
  KCHEM_NOXADV=0_JPIM
  KCHEM_WETDEP=1_JPIM
  LCHEM_ANAO3=.FALSE.
  LCHEM_ANACH4=.FALSE.
  LCHEM_WEAK_CH4_RELAXATION=.FALSE.
  LCHEM_FIX_CH4=.FALSE.
  LCHEM_O3RAD=.FALSE.
  LCHEM_AEROI=.FALSE.
  LCHEM_WDFR=.TRUE.
  LCHEM_CONVSCAV=.FALSE.
  LCHEM_CSHAPE=.FALSE.
  LCHEM_CSHAPE98=.FALSE.
  LCHEM_TL=.FALSE.
  LCHEM_VSO2_COUPLE=.FALSE.
  KCHEM_SOLVE=1
  KCHEM_YEARPI=2000
 
  RCH4CONST=-9999.9_JPRB

  NCHEM_LCOCOEF=5
  LCHEM_LCOLIMIT=.FALSE.
  LCHEM_LCOMESO=.TRUE.
  LCHEM_LCOCSTCLIM=.TRUE.
  RCHEM_LCOCLIMTOP=6.E-07_JPRB
  RCHEM_LCOTAUTOP=7._JPRB
  RCHEM_LCOCOEFA1=1._JPRB
  CHEM_LCOVERS='_V1.2'

  ! default values for ASIS method parameters in namchem:
  CSOLMET_ASIS='GMRES'   ! GMRES is normally the most efficient method (better than GSEID and DGESV)
  M_soliter_ASIS=15      ! 10 is normally sufficient for GMRES and solcv=1e-14; 20 is recommended for GSEID

  SOLCV_ASIS=1.E-14_JPRB ! 1e-14 is correct, 1e-15 is better for some species (BrOx and ClOx)
  RTOL_ASIS=0.025        ! 0.025 is a good compromise
  N_F_RTOL_ASIS=2
  ATOL_ASIS=1E+4_JPRB
  !N_F_ATOL_ASIS=1        ! 1 is recommended (ATOL=1e-13*air density,  ATOL_ASIS in fact not used)
  N_F_ATOL_ASIS=11        ! 1 is recommended (ATOL=1e-13*air density,  ATOL_ASIS in fact not used)

  LCHEM_ARPCLIM=.FALSE.
  LCHEM_EMICH4=.FALSE.

  CHEM_SCHEME='arpclim_repro'

  CALL POSNAM(NULNAM,'NAMCHEM')
  READ(NULNAM,NAMCHEM)
  
  IF (LCHEM_LIGHT) THEN
    ! Testing if EMILI GFL found from NAMGFL
    ITRC=0
    DO JGFL=1,NGFL_EXT
      IF ( TRIM(YGFL%YEXT_NL(JGFL)%CNAME) == 'EMILI')    ITRC   = JGFL
    ENDDO
    IF (ITRC < 1) CALL ABOR1("SUGFL1: EXTRA GFL 'EMILI' not found, required for LCHEM_LIGHT=.TRUE.")

    WRITE(NULOUT,'(A,I4)') ' NOxLIGHTNING MODE = ', YDEPHY%NLIMODE

    IF (LCHEM_CSHAPE98) LCHEM_CSHAPE=.TRUE.
  ENDIF

  ! To workaround a PGI compiler bug, CHEM_SCHEME is not a pointer into YDCHEM.
  YDCHEM%CHEM_SCHEME  = CHEM_SCHEME
  YDCHEM%CHEM_LCOVERS = CHEM_LCOVERS

  WRITE(NULOUT,*) ' SUGFL1: printing NAMCHEM keys:'
  WRITE(NULOUT,'(A,L2,A,f10.3,A,L2,A,L2,A,L2,A,L2,A,L2,A,L2,A,L2)')&
   & '  LCHEM_LIGHT=',LCHEM_LIGHT,&
   & '  RCHEM_LINOX_SCALING=',RCHEM_LINOX_SCALING,&
!   & '  LCHEM_DIA=',LCHEM_DIA, &
   & '  LCHEM_DIAC=',LCHEM_DIAC,&
   & '  LCHEM_BASCOE_JON=',LCHEM_BASCOE_JON,&
   & '  LCHEM_EXTENDJNO2=',LCHEM_EXTENDJNO2,&
   & '  LCHEM_BASCOE_HET=',LCHEM_BASCOE_HETCHEM,&
   & '  LMOZART_RSF_DUMMY=',LMOZART_RSF_DUMMY,&
   & '  LCHEM_JOUT=',LCHEM_JOUT
  WRITE(NULOUT,'(4(A,I2),A,I6,11(A,L2))')&
   & '  KCHEM_NOXADV=',KCHEM_NOXADV,&
   & '  KCHEM_WETDEP=',KCHEM_WETDEP,&
   & '  KCHEM_DRYDEP=',KCHEM_DRYDEP,&
   & '  KCHEM_SOLVE=',KCHEM_SOLVE,&
   & '  KCHEM_YEARPI=',KCHEM_YEARPI,&
   & '  LCHEM_ANAO3=',LCHEM_ANAO3,&
   & '  LCHEM_FIX_CH4=',LCHEM_FIX_CH4,&
   & '  LCHEM_O3RAD=',LCHEM_O3RAD,&
   & '  LCHEM_CSHAPE=',LCHEM_CSHAPE,&
   & '  LCHEM_CSHAPE98=',LCHEM_CSHAPE98,&   
   & '  LCHEM_WDFR=',LCHEM_WDFR,&  
   & '  LCHEM_AEROI=',LCHEM_AEROI,&
   & '  LCHEM_TL=',LCHEM_TL, &
   & '  LCHEM_VSO2_COUPLE=',LCHEM_VSO2_COUPLE, &
   & '  LCHEM_ARPCLIM=',LCHEM_ARPCLIM, &
   & '  LCHEM_EMICH4=',LCHEM_EMICH4

  IF (TRIM(YDCHEM%CHEM_SCHEME) == "linco" ) THEN 
    WRITE(NULOUT,'(A,I2,A,L2,A,L2,A,L2,A,f10.3,A,f10.3,A,f10.3)')&
     & '  NCHEM_LCOCOEF=',NCHEM_LCOCOEF,&
     & '  LCHEM_LCOLIMIT=',LCHEM_LCOLIMIT,&
     & '  LCHEM_LCOMESO=',LCHEM_LCOMESO,&
     & '  LCHEM_LCOCSTCLIM=',LCHEM_LCOCSTCLIM,&
     & '  RCHEM_LCOCLIMTOP=',RCHEM_LCOCLIMTOP,&
     & '  RCHEM_LCOTAUTOP=',RCHEM_LCOTAUTOP,&
     & '  RCHEM_LCOCOEFA1=',RCHEM_LCOCOEFA1
  ENDIF

  IF (LCHEM_FIX_CH4)  WRITE(NULOUT,'(A,f10.3)') ' CONST CH4 in PPB VMR ', RCH4CONST*1.0E9_JPRB 

  IF (LCHEM_AEROI .AND. NACTAERO == 0 ) CALL ABOR1('SUGFL1: LCHEM_AEROI requires active aerosol ')

  IF (KCHEM_DRYDEP >0 .AND. .NOT. LCOMPO_DDFLX_DIR ) CALL ABOR1('SUGFL1: KCHEM_DRYDEP >0 requires   LCOMPO_DDFLX_DIR = true ')

ELSE

  KCHEM_DRYDEP=0
  LCHEM_LIGHT=.FALSE.
  RCHEM_LINOX_SCALING=0.0_JPRB
  LCHEM_DIAC=.FALSE.
  LCHEM_JOUT=.FALSE.
  LCHEM_BASCOE_JON=.FALSE.
  LCHEM_BASCOE_HETCHEM=.TRUE.
  LMOZART_RSF_DUMMY=.FALSE.
  LCHEM_EXTENDJNO2=.FALSE.
  KCHEM_NOXADV=0_JPIM
  KCHEM_WETDEP=1_JPIM
  LCHEM_ANAO3=.FALSE.
  LCHEM_ANACH4=.FALSE.
  LCHEM_WEAK_CH4_RELAXATION=.FALSE.
  LCHEM_FIX_CH4=.FALSE.
  LCHEM_CONVSCAV=.FALSE.
  LCHEM_CSHAPE=.FALSE.
  LCHEM_CSHAPE98=.FALSE.
  LCHEM_WDFR=.FALSE.
  LCHEM_O3RAD=.FALSE.
  LCHEM_AEROI=.FALSE.
  LCHEM_TL=.FALSE.
  LCHEM_VSO2_COUPLE=.FALSE.
  KCHEM_SOLVE=0
  KCHEM_YEARPI=2000
  RCH4CONST=-9999.9_JPRB
  LCHEM_ARPCLIM=.FALSE.
  LCHEM_EMICH4=.FALSE.

  
  WRITE(NULOUT,*) ' SUGFL1: all NAMCHEM keys are set to F '

ENDIF

! AER namelist 
IF (NACTAERO > 0) THEN
  !MelA: Setup NAEROCV
  DO JGFL = 1, NAERO
    IF (YAERO_NL(JGFL)%LASSIM ) THEN
      NAEROCV=NAEROCV + 1
      IF (NAEROCV > JPAEROCV) CALL ABOR1('SUDIM1: Too many assimilated AEROCV fields. Increase JPAEROCV.')
      NGRBAERCV(NAEROCV) = YAERO_NL(JGFL)%IGRBCODE
    ENDIF
  ENDDO
  ! Set up aerosol, requires calling "early" if progostic aerosols in use
  CALL SU_AERW(YDMODEL)
ELSE
  ! otherwise just ensure this isn't a dangling pointer
  ALLOCATE(YDMODEL%YRML_PHY_RAD%YREAERATM%YAERO_DESC(1))
ENDIF

!     ------------------------------------------------------------------

!*       8. COMPUTE THE TOTAL NUMBER OF "GEMS" FIELDS IN CONTROL VECTOR.
!           ------------------------------------------

NGEMS=NGHG_ASSIM+NAERO+NCHEM_ASSIM


!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUGFL1',1,ZHOOK_HANDLE)
END SUBROUTINE SUGFL1

