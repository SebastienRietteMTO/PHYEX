SUBROUTINE APL_AROME_SHALLOW(YDCST, YDMODEL, YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDMF_PHYS, &
                           & YDCPG_BNDS, YDCPG_OPTS, YDVARS, YSPP_ALL, YDDDH, &
                           & PSFTH, PSFRV, PSFU, PSFV, PEXNREFM, PWCLDFR, PTKEM, &
                           & PTM, PQVM, PQCM, PQIM, PQDM, &
                           & PRM, PSVM, &
                           & PAPHIM, PAPHIFM, &
                           & PZZ, PZZ_F, &
                           & PRHODJM, PRHODREFM, PPABSM, &
                           & PTHM, PUM, PVM, &
                           & PRS, PTHS, PTKES, &
                           & PTENDTKE, PTENDT, PTENDTT, PTEND_Q, PTEND_R, PTEND_S, &
                           & PUS, PVS, PTKEMX, &
                           & PMF_UP, &
                           & PSURFPREP, PSURFSNOW, &
                           & PLENGTH_M, PLENGTH_H, PTKEEDMFS, &
                           & PFLXZTHVMF_SUM, PFLXZUMF_SUM, PFLXZVMF_SUM, PSIGMF)

!**** *APL_AROME_SHALLOW* - Setup and call Arome shallow convection

!     Author.
!     -------
!      Rolf Heilemann Myhre *Met Norway*
!      Original : 01-09-2023
!      See APL_AROME for modification history

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------
!
!   YDCST:                Model constants
!   YDMODEL:              Model configuration settings
!   YDMF_PHYS_BASE_STATE:
!   YDGEOMETRY:           Geomtery settings from IFS model
!   YDMF_PHYS:            Physics fields
!   YDCPG_BNDS:           Array dimensions and bounds
!   YDCPG_OPTS:           Various dimensions
!   YSPP_ALL:             SPP fields and settings
!   YDDDH:                DDH fields and settings
!
!   PTM:    Temperature
!   PRM:    Moist variables at time t
!   PTKEM:  Turbulent kinetic energy
!   PSVM:   Passive scalars
!   PUM:    Zonal wind
!   PVM:    Meridian wind
!   PTENDT: Temperature tendency
!

  USE PARKIND1, ONLY: JPRB, JPIM

  USE YOMCST,                      ONLY: TCST
  USE TYPE_MODEL,                  ONLY: MODEL
  USE MF_PHYS_BASE_STATE_TYPE_MOD, ONLY: MF_PHYS_BASE_STATE_TYPE
  USE GEOMETRY_MOD,                ONLY: GEOMETRY
  USE MF_PHYS_TYPE_MOD,            ONLY: MF_PHYS_TYPE
  USE CPG_OPTS_TYPE_MOD,           ONLY: CPG_BNDS_TYPE, CPG_OPTS_TYPE
  USE FIELD_VARIABLES_MOD,         ONLY: FIELD_VARIABLES
  USE SPP_MOD_TYPE,                ONLY: UA_PHYS_SPP_VARS
  USE DDH_MIX,                     ONLY: TYP_DDH

  USE YOMHOOK,                     ONLY: LHOOK, DR_HOOK, JPHOOK

  IMPLICIT NONE

  TYPE(TCST),                    INTENT(IN) :: YDCST
  TYPE(MF_PHYS_BASE_STATE_TYPE), INTENT(IN) :: YDMF_PHYS_BASE_STATE
  TYPE(GEOMETRY),                INTENT(IN) :: YDGEOMETRY
  TYPE(MF_PHYS_TYPE),            INTENT(IN) :: YDMF_PHYS
  TYPE(MODEL),                   INTENT(IN) :: YDMODEL
  TYPE(CPG_BNDS_TYPE),           INTENT(IN) :: YDCPG_BNDS
  TYPE(CPG_OPTS_TYPE),           INTENT(IN) :: YDCPG_OPTS
  TYPE(FIELD_VARIABLES),         INTENT(IN) :: YDVARS

  TYPE(UA_PHYS_SPP_VARS),        INTENT(INOUT) :: YSPP_ALL
  TYPE(TYP_DDH),                 INTENT(INOUT) :: YDDDH


  REAL(KIND=JPRB), INTENT(IN)    :: PSFTH(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(IN)    :: PSFRV(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(IN)    :: PSFU(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(IN)    :: PSFV(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(IN)    :: PEXNREFM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PWCLDFR(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PTKEM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PTM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PQVM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PQCM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PQIM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PQDM(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PRM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(IN)    :: PSVM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB), INTENT(IN)    :: PAPHIM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PAPHIFM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PZZ(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PZZ_F(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PRHODJM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PRHODREFM(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PPABSM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PTHM(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PUM(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PVM(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PRS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTHS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTKES(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDTKE(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! temperature tendency
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDTT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! array to save heating profile for LHN
  REAL(KIND=JPRB), INTENT(INOUT) :: PTEND_Q(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTEND_R(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTEND_S(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PUS(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PVS(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTKEMX(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PMF_UP(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(OUT)   :: PSURFPREP(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB), INTENT(OUT)   :: PSURFSNOW(YDCPG_OPTS%KLON)

  REAL(KIND=JPRB), INTENT(OUT)   :: PLENGTH_M(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(OUT)   :: PLENGTH_H(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(OUT)   :: PTKEEDMFS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), TARGET, INTENT(OUT) :: PFLXZTHVMF_SUM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), TARGET, INTENT(OUT) :: PFLXZUMF_SUM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), TARGET, INTENT(OUT) :: PFLXZVMF_SUM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(OUT)   :: PSIGMF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)


  REAL(KIND=JPRB) :: ZTHETALX_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)
  REAL(KIND=JPRB) :: ZQT_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)
  REAL(KIND=JPRB) :: ZTHTV_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)

  REAL(KIND=JPRB) :: ZMF_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)
  REAL(KIND=JPRB) :: ZQC_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)
  REAL(KIND=JPRB) :: ZQI_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)
  REAL(KIND=JPRB) :: ZU_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)
  REAL(KIND=JPRB) :: ZV_UP(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KMAXDRAFT)

  REAL(KIND=JPRB) :: ZZS_FTH(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZZS_FRV(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZZEXNREFM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZZWCLDFR(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  ! Variables used in case LHARATU=TRUE
  ! length scales for momentum and heat and TKE
  REAL(KIND=JPRB) :: ZTKEEDMF(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZLENGTH_M(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZLENGTH_H(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  ! Variables concerning updraft rain/snow for EDMF
  REAL(KIND=JPRB) :: ZTENDTUP(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZTENDQVUP(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZUPGENL(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZUPGENN(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZZU_UP(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZZV_UP(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZRT_UP(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZRC_UP(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZRI_UP(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZZW_UP(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZZFRAC_UP(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZTHETAL_UP(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZTHETAV_UP(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZRC_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZRI_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZCF_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLC_HRC_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLC_HCF_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLI_HRI_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLI_HCF_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZWEIGHT_MF_CLOUD(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZCLFR(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZZRV_UP(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), TARGET :: ZFLXZTHVMF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), TARGET :: ZFLXZUMF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), TARGET :: ZFLXZVMF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZMFUS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZMFVS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZMFTKES(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZTHLS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZRTS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZSVXXX(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB) :: ZDZZ_F(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), POINTER :: ZARG_FLXZTHVMF(:,:)
  REAL(KIND=JPRB), POINTER :: ZARG_FLXZUMF(:,:)
  REAL(KIND=JPRB), POINTER :: ZARG_FLXZVMF(:,:)

  INTEGER(KIND=JPIM) :: JLON, JLEV, JDRAFT
  INTEGER(KIND=JPIM), PARAMETER :: IKL = -1

  REAL(KIND=JPRB) :: ZRSCP, ZINVATM
  REAL(KIND=JPRB) :: ZDT, ZINVG

  INTEGER(KIND=JPIM) :: IDRAFT, INDRAFT

  REAL(KIND=JPRB), PARAMETER :: PPTKEMIN = 1.E-6

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  !------------------------------------------------------------------

#include "abor1.intfb.h"
#include "vdfhghthl.intfb.h"
#include "aro_shallow_mf.h"

  !------------------------------------------------------------------

  IF (LHOOK) CALL DR_HOOK('APL_AROME_SHALLOW_MOD:APL_AROME_SHALLOW', 0, ZHOOK_HANDLE)

  ZRSCP   = YDCST%RD/YDCST%RCPD
  ZINVATM = 1._JPRB/YDCST%RATM
  ZINVG   = 1._JPRB/YDCST%RG

  IF (YDMODEL%YRML_DYN%YRDYNA%LTWOTL) THEN
    ZDT=YDCPG_OPTS%ZDTPHY/2._JPRB
  ELSE
    IF (YDCPG_OPTS%NSTEP/=0) THEN
      ZDT=YDCPG_OPTS%ZDTPHY/2._JPRB
    ELSE
      ZDT=YDCPG_OPTS%ZDTPHY
    ENDIF
  ENDIF

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_UPDRAFT == 'DUAL') THEN

    ! Updraft computation from EDMF/ECMWF dual proposal
    ! Version May 2007
    !
    ! The following routine  are using arrays with the vertical Arpege/IFS fashion (as in the radiation scheme)

    IDRAFT  = 2 ! beginning of the loop for MF tendency equation
                ! only 2 and 3 are used for tendency computation in ARO_SHALLOW_MF
    INDRAFT = 3 ! 1 for test, 2 for dry, 3 for wet

    IF (YDCPG_OPTS%KMAXDRAFT < INDRAFT) THEN
      CALL ABOR1('APL_AROME : KMAXDRAFT TOO SMALL !')
    ENDIF

    DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      ZZS_FTH(JLON) = -1._JPRB*PSFTH(JLON)*(YDMF_PHYS_BASE_STATE%YCPG_PHY%PRE(JLON,YDCPG_OPTS%KFLEVG)*ZINVATM)**(ZRSCP)
      ZZS_FRV(JLON) = -1._JPRB*PSFRV(JLON)
    ENDDO

    DO JLEV=1,YDCPG_OPTS%KFLEVG
      ZZEXNREFM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = PEXNREFM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
    ENDDO

    ! If OCND2 and non-LIMA microphysics use water or mixed-phase cloud fraction for VDFHGHTHL,
    ! else use cloud fraction YDVARS%A%T1
    IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_ICEN%LOCND2 .AND. &
      & YDMODEL%YRML_PHY_MF%YRARPHY%LMICRO .AND. &
      & YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO /= 'LIMA') THEN
      DO JLEV=1,YDCPG_OPTS%KFLEVG
        ZZWCLDFR(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = PWCLDFR(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
      ENDDO
    ELSE
      DO JLEV=1,YDCPG_OPTS%KFLEVG
        ZZWCLDFR(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = YDVARS%A%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
      ENDDO
    ENDIF

    ! IF LHARATU=TRUE then TKE at t-dt is needed as input for vdfexcuhl so fill ZTKEEDMF with t-1 value  from PTKEM
    IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%TURBN%LHARAT) THEN
      DO JLEV=1,YDCPG_OPTS%KFLEVG
        ZTKEEDMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)=PTKEMX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZLENGTH_M(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)=0.01_JPRB
        ZLENGTH_H(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)=0.01_JPRB
      ENDDO
    ENDIF

    CALL VDFHGHTHL(YDMODEL%YRML_PHY_G%YRVDF,                                 &
    & YDMODEL%YRML_PHY_SLIN%YREPHLI,                                         &
    & YDMODEL%YRML_PHY_EC%YRECUMF,                                           &
    & YDMODEL%YRML_PHY_EC%YREPHY,                                            &
    & YDMODEL%YRML_PHY_MF%YRPARAR,                                           &
    & YDGEOMETRY%YREGEO,                                                     &
    & YDCPG_OPTS%NSTEP, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA,                  &
    & YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, INDRAFT,                           &
    & YDCPG_OPTS%ZDTPHY, YDMF_PHYS_BASE_STATE%U, YDMF_PHYS_BASE_STATE%V,     &
    & PTM, PQVM, PQCM, PQIM, ZZWCLDFR,                                       &
    & YDMF_PHYS_BASE_STATE%YCPG_PHY%PRE, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREF, &
    & PAPHIFM, PAPHIM, ZZEXNREFM,                                            &
    & ZZS_FTH, ZZS_FRV, PSFU, PSFV,                                          &
    & ZMF_UP, ZTHETAL_UP, ZQT_UP, ZTHTV_UP,                                  &
    & ZQC_UP, ZQI_UP, ZU_UP, ZV_UP,                                          &
    & YSPP_ALL%YSPP_CLDDPTH, YSPP_ALL%YSPP_CLDDPTHDP,                        &
    & YSPP_ALL%YSPP_RFAC_TWOC, YSPP_ALL%YSPP_RZC_H, YSPP_ALL%YSPP_RZL_INF,   &
    & ZTENDQVUP, ZTENDTUP, PSURFPREP, PSURFSNOW,                             &
    & ZUPGENL, ZUPGENN, ZCLFR,                                               &
    & ZLENGTH_M, ZLENGTH_H, ZTKEEDMF)

    !  tendtup, tendqvup  tendencies for non-conserved AROME
    !  variables due to updraft precipitation/snow (and its evaporation)
    DO JLEV = 2 ,YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PTENDT(JLON,JLEV)  = PTENDT(JLON,JLEV)  + ZTENDTUP(JLON,JLEV)
        PTEND_Q(JLON,JLEV) = PTEND_Q(JLON,JLEV) + ZTENDQVUP(JLON,JLEV)
      ENDDO
    ENDDO

    IF (YDMODEL%YRML_PHY_MF%YRPARAR%LTOTPREC .OR. YDMODEL%YRML_PHY_MF%YRPARAR%LTOTPRECL) THEN

      !Add rain and snow tendencies from the sub-grid scheme to tendencies and sources,
      !at all vertical levels, instead of diagnosing only surface precip.
      PSURFPREP(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = 0.0_JPRB
      PSURFSNOW(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = 0.0_JPRB

      DO JLEV= 1, YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          !Add rain and snow to sources:
          PRS(JLON,JLEV,3) = PRS(JLON,JLEV,3) + ZUPGENL(JLON,JLEV)
          PRS(JLON,JLEV,5) = PRS(JLON,JLEV,5) + ZUPGENN(JLON,JLEV)
          PTHS(JLON,JLEV)  = PTHS(JLON,JLEV) + &
          & ZTENDTUP(JLON,JLEV)*(YDCST%RATM/YDMF_PHYS_BASE_STATE%YCPG_PHY%PREF(JLON,JLEV))**(YDCST%RD/YDCST%RCPD)
          !Update rain/snow tendencies:
          PTEND_R(JLON,JLEV) = PTEND_R(JLON,JLEV) + ZUPGENL(JLON,JLEV)
          PTEND_S(JLON,JLEV) = PTEND_S(JLON,JLEV) + ZUPGENN(JLON,JLEV)
        ENDDO
      ENDDO
    ENDIF

  ELSE

    IDRAFT  = 3 ! only a wet updraft
    INDRAFT = 1
    PSURFPREP(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = 0._JPRB
    PSURFSNOW(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = 0._JPRB

  ENDIF

  DO JLEV = 1, YDCPG_OPTS%KFLEVG - 1
    DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      ZDZZ_F(JLON,JLEV) = PZZ_F(JLON,JLEV) - PZZ_F(JLON,JLEV+1)
    ENDDO
  ENDDO
  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
    ZDZZ_F(JLON,YDCPG_OPTS%KFLEVG) = PZZ_F(JLON,YDCPG_OPTS%KFLEVG) - YDVARS%GEOMETRY%OROG%T0(JLON)*ZINVG
  ENDDO

  DO JDRAFT=IDRAFT,3

    !!! Call mass fluxes computations
    ! If CMF_UPDRAFT='DUAL', the updraft characteritics are already computed and will be passed as inputs of SHALLOW_MF
    ! if not, they will be computed in SHALLOW_MF itself (from Méso-NH type routines)

    ! JDRAFT=2 : dry updraft
    ! JDRAFT=3 : wet updraft

    IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_UPDRAFT=='DUAL') THEN

      ! Goes from one of the updraft from the IFS level world to the Méso-NH level world
      ! go from q to r)
      DO JLEV = 1,YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
          PMF_UP(JLON,JLEV)     = ZMF_UP(JLON,JLEV,JDRAFT)
          ZZU_UP(JLON,JLEV)     = ZU_UP(JLON,JLEV,JDRAFT)
          ZZV_UP(JLON,JLEV)     = ZV_UP(JLON,JLEV,JDRAFT)
          ZTHETAL_UP(JLON,JLEV) = ZTHETALX_UP(JLON,JLEV,JDRAFT)
          ZTHETAV_UP(JLON,JLEV) = ZTHTV_UP(JLON,JLEV,JDRAFT)
          ZRT_UP(JLON,JLEV)     = ZQT_UP(JLON, JLEV, JDRAFT)/(1. - ZQT_UP(JLON, JLEV, JDRAFT))
          ZRC_UP(JLON,JLEV)     = ZQC_UP(JLON, JLEV, JDRAFT)/(1. - ZQT_UP(JLON, JLEV, JDRAFT))
          ZRI_UP(JLON,JLEV)     = ZQI_UP(JLON, JLEV, JDRAFT)/(1. - ZQT_UP(JLON, JLEV, JDRAFT))
        ENDDO
      ENDDO

      ZZW_UP(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG) = 0._JPRB
      ZZFRAC_UP(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG) = 0._JPRB
      IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%TURBN%LHARAT) THEN
        DO JLEV = 1,YDCPG_OPTS%KFLEVG
          DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
            PLENGTH_M(JLON,JLEV) = MAX(0.01_JPRB, ZLENGTH_M(JLON,JLEV))
            PLENGTH_H(JLON,JLEV) = MAX(0.01_JPRB, ZLENGTH_H(JLON,JLEV))
            ! TKE should be bigger than a minimum value:
            PTKEEDMFS(JLON,JLEV) = MAX(ZTKEEDMF(JLON,JLEV),PPTKEMIN)/YDCPG_OPTS%ZDTPHY
          ENDDO
        ENDDO
      ENDIF
    ENDIF

    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      ZRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
      ZRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
      ZCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
    ENDDO

    IF (JDRAFT == IDRAFT) THEN
      ! Fill the sum at the first iteration
      ZARG_FLXZTHVMF => PFLXZTHVMF_SUM(:,1:YDCPG_OPTS%KFLEVG)
      ZARG_FLXZUMF => PFLXZUMF_SUM(:,1:YDCPG_OPTS%KFLEVG)
      ZARG_FLXZVMF => PFLXZVMF_SUM(:,1:YDCPG_OPTS%KFLEVG)
    ELSE
      ! increment
      ZARG_FLXZTHVMF => ZFLXZTHVMF(:,1:YDCPG_OPTS%KFLEVG)
      ZARG_FLXZUMF => ZFLXZUMF(:,1:YDCPG_OPTS%KFLEVG)
      ZARG_FLXZVMF => ZFLXZVMF(:,1:YDCPG_OPTS%KFLEVG)
    ENDIF
    CALL ARO_SHALLOW_MF(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & KKL=IKL, KLON=YDCPG_BNDS%KFDIA, KLEV=YDCPG_OPTS%KFLEVG, KFDIA=YDCPG_BNDS%KFDIA, &
    & KRR=YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & KRRL=YDMODEL%YRML_PHY_MF%YRPARAR%NRRL, &
    & KRRI=YDMODEL%YRML_PHY_MF%YRPARAR%NRRI, &
    & KSV=YDMODEL%YRML_GCONF%YGFL%NGFL_EXT, &
    & KSV_LGBEG=0, KSV_LGEND=0, PTSTEP=ZDT, &
    & PDX=YDGEOMETRY%YREGEO%EDELX, PDY=YDGEOMETRY%YREGEO%EDELY, &
    & PZZ=PZZ, PZZF=PZZ_F, PDZZF=ZDZZ_F, &
    & PRHODJ=PRHODJM(:, 1:YDCPG_OPTS%KFLEVG), PRHODREF=PRHODREFM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PPABSM=PPABSM(:, 1:YDCPG_OPTS%KFLEVG), PEXNM=PEXNREFM, PSFTH=PSFTH, PSFRV=PSFRV, PTHM=PTHM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PRM=PRM, PUM=PUM(:, 1:YDCPG_OPTS%KFLEVG), PVM=PVM(:, 1:YDCPG_OPTS%KFLEVG), PTKEM=PTKEM(:, 1:YDCPG_OPTS%KFLEVG),     &
    & PSVM=PSVM, PDUDT_MF=ZMFUS, PDVDT_MF=ZMFVS, PDTKEDT_MF=ZMFTKES, PDTHLDT_MF=ZTHLS, PDRTDT_MF=ZRTS, PDSVDT_MF=ZSVXXX, &
    & PSIGMF=PSIGMF, PRC_MF=ZRC_MF, PRI_MF=ZRI_MF, PCF_MF=ZCF_MF, &
    & PHLC_HRC_MF=ZHLC_HRC_MF, PHLC_HCF_MF=ZHLC_HCF_MF, PHLI_HRI_MF=ZHLI_HRI_MF, PHLI_HCF_MF=ZHLI_HCF_MF, &
    & PWEIGHT_MF_CLOUD=ZWEIGHT_MF_CLOUD, PFLXZTHVMF=ZARG_FLXZTHVMF, PFLXZUMF=ZARG_FLXZUMF, PFLXZVMF=ZARG_FLXZVMF, PTHL_UP=ZTHETAL_UP, &
    & PRT_UP=ZRT_UP, PRV_UP=ZZRV_UP, PRC_UP=ZRC_UP, PRI_UP=ZRI_UP, PU_UP=ZZU_UP, PV_UP=ZZV_UP,                            &
    & PTHV_UP=ZTHETAV_UP, PW_UP=ZZW_UP, PFRAC_UP=ZZFRAC_UP, PEMF=PMF_UP(:, 1:YDCPG_OPTS%KFLEVG),                          &
    & YDDDH=YDDDH, YDLDDH=YDMODEL%YRML_DIAG%YRLDDH, YDMDDH=YDMODEL%YRML_DIAG%YRMDDH)

    !wc No variance due to dry updraft yet.
    ! Putting PSIGMF to 0 for dry updraft might be obsolete
    IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSTATNW) THEN
      IF (JDRAFT == 2) THEN
        DO JLEV = 1,YDCPG_OPTS%KFLEVG
          PSIGMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = 0._JPRB
        ENDDO
      ENDIF
    ENDIF

    IF (JDRAFT > IDRAFT) THEN
      ! Add increment
      PFLXZTHVMF_SUM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
      & PFLXZTHVMF_SUM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) + &
      & ZFLXZTHVMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)
      PFLXZUMF_SUM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
      & PFLXZUMF_SUM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) + &
      & ZFLXZUMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)
      PFLXZVMF_SUM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
      & PFLXZVMF_SUM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) + &
      & ZFLXZVMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)
    ENDIF

    ! traitement des sorties pour repasser dans le monde Aladin

    IF ((YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'DIRE' .OR. &
       & YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'BIGA') .AND. JDRAFT==3) THEN
      ! sauvegarde pour le schema de nuage
      DO JLEV = 1,YDCPG_OPTS%KFLEVG
        YDVARS%LMF%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        YDVARS%AMF%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        YDVARS%IMF%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        YDVARS%WMFC%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZWEIGHT_MF_CLOUD(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        YDVARS%HLMF%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZHLC_HRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        YDVARS%HLCFMF%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZHLC_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        YDVARS%HIMF%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZHLI_HRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        YDVARS%HICFMF%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = ZHLI_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
      ENDDO
    ENDIF

    DO JLEV = 1,YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PUS(JLON,JLEV) = PUS(JLON,JLEV) + ZMFUS(JLON,JLEV)
        PVS(JLON,JLEV) = PVS(JLON,JLEV) + ZMFVS(JLON,JLEV)
        PTHS(JLON,JLEV)  = PTHS(JLON,JLEV)  + ZTHLS(JLON,JLEV)
        PRS(JLON,JLEV,1) = PRS(JLON,JLEV,1) + ZRTS(JLON,JLEV)
        PTKES(JLON,JLEV) = PTKES(JLON,JLEV) + ZMFTKES(JLON,JLEV)

        !calcul de tendance et inversion des niveaux pour le vent horizontal
        YDMF_PHYS%OUT%TENDU(JLON,JLEV) = YDMF_PHYS%OUT%TENDU(JLON,JLEV) + ZMFUS(JLON,JLEV)
        YDMF_PHYS%OUT%TENDV(JLON,JLEV) = YDMF_PHYS%OUT%TENDV(JLON,JLEV) + ZMFVS(JLON,JLEV)

        !conversion de la tendance de theta en tendance de T et inversion niveau
        PTENDT(JLON,JLEV)  = PTENDT(JLON,JLEV)  + ZTHLS(JLON,JLEV)*PEXNREFM(JLON,JLEV)
        PTENDTT(JLON,JLEV) = PTENDTT(JLON,JLEV) + ZTHLS(JLON,JLEV)

        !inversion niveaux tendances des ri et conversion en qi en multipliant par qd
        PTEND_Q(JLON,JLEV) = PTEND_Q(JLON,JLEV) + ZRTS(JLON,JLEV)*PQDM(JLON,JLEV)

        PTENDTKE(JLON,JLEV) = PTENDTKE(JLON,JLEV) + ZMFTKES(JLON,JLEV)
      ENDDO
    ENDDO

  ENDDO ! JDRAFT

  IF (LHOOK) CALL DR_HOOK('APL_AROME_SHALLOW_MOD:APL_AROME_SHALLOW', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_AROME_SHALLOW
